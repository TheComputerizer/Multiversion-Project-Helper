import static org.gradle.api.file.DuplicatesStrategy.EXCLUDE

plugins {
    id 'groovy-gradle-plugin'
    id 'java-library'
    id 'idea'
}

group = project_package
version = project_version

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        inheritOutputDirs = true
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of 23
}

repositories {
    gradlePluginPortal() {
        content {
            [ 'com.fasterxml', 'com.fasterxml.jackson', 'com.fasterxml.woodstox', 'com.google.code.findbugs',
              'com.google.code.gson', 'com.google.errorprone', 'com.google.guava', 'com.google.j2objc',
              'com.machinezoo.noexception', 'commons-codec', 'commons-io', 'commons-logging', 'de.siegmar',
              'dev.gradleplugins', 'io.fabric8', 'io.github.goooler.shadow', 'io.github.goooler.shadow', 'io.netty',
              'it.unimi.dsi', 'jakarta.platform', 'net.sf.jopt-simple', 'org.apache', 'org.apache.ant',
              'org.apache.commons', 'org.apache.httpcomponents', 'org.apache.logging', 'org.apache.logging.log4j',
              'org.apache.maven', 'org.cadixdev', 'org.checkerframework', 'org.codehaus.groovy', 'org.codehaus.plexus',
              'org.codehaus.woodstox', 'org.eclipse.ee4j', 'org.eclipse.jetty', 'org.gradle.toolchains',
              'org.gradle.toolchains.foojay-resolver-convention', 'org.jdom', 'org.jetbrains', 'org.jetbrains.kotlin',
              'org.junit', 'org.mockito', 'org.ow2', 'org.ow2.asm', 'org.slf4j', 'org.sonatype.oss',
              'org.springframework', 'org.tukaani', 'org.vafer' ].forEach { includeGroup it }
        }
    }
    mavenCentral() {
        content {
            [ 'org.apache.groovy' ].forEach { includeGroup it }
        }
    }
    maven {
        name = 'Fabric'
        url = uri('https://maven.fabricmc.net/')
        content {
            [ 'fabric-loom', 'net.fabricmc', 'net.fabricmc.fabric-api' ].forEach { includeGroup it }
        }
    }
    maven {
        name = 'Forge'
        url = uri('https://maven.minecraftforge.net/')
        content {
            [ 'net.minecraftforge', 'net.minecraftforge.gradle',
              'org.spongepowered' ].forEach{ includeGroup it }
        }
    }
    maven {
        name = 'Garden of Fancy'
        url = uri('https://maven.gofancy.wtf/releases')
        content {
            includeGroup 'wtf.gofancy.fancygradle'
        }
    }
    maven {
        name = 'ParchmentMC'
        url = uri('https://maven.parchmentmc.org')
        content {
            [ 'org.parchmentmc', 'org.parchmentmc.feather',
              'org.parchmentmc.librarian.forgegradle' ].forEach { includeGroup it }
        }
    }
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation gradleTestKit()
    implementation "fabric-loom:fabric-loom.gradle.plugin:$loom_version"
    implementation 'io.github.goooler.shadow:shadow-gradle-plugin:8.+'
    implementation 'net.minecraftforge.gradle:net.minecraftforge.gradle.gradle.plugin:6.+'
    implementation 'org.gradle.toolchains.foojay-resolver-convention:org.gradle.toolchains.foojay-resolver-convention.gradle.plugin:0.+'
    implementation 'org.parchmentmc.librarian.forgegradle:org.parchmentmc.librarian.forgegradle.gradle.plugin:1.+'
    implementation 'org.spongepowered:mixingradle:0.+'
    implementation 'wtf.gofancy.fancygradle:wtf.gofancy.fancygradle.gradle.plugin:1.+'
}

compileJava {
    options.deprecation = false //We don't need to log all of the deprecated stuff when compiling
    options.encoding = 'UTF-8'
}

processResources {
    duplicatesStrategy = EXCLUDE
}

jar {
    duplicatesStrategy = EXCLUDE
    setArchiveBaseName project_id
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = "${project_id}.settings"
            implementationClass = "${project_package}.settings.SettingsPlugin"
        }
        //simplePlugin {
        //    id = "${project_id}.fabric"
        //    implementationClass = "${project_package}.fabric.FabricPlugin"
        //}
        //simplePlugin {
        //    id = "${project_id}.forge"
        //    implementationClass = "${project_package}.forge.ForgePlugin"
        //}
    }
}