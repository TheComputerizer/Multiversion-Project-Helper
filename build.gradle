import static org.gradle.api.file.DuplicatesStrategy.EXCLUDE

plugins {
    id 'groovy-gradle-plugin'
    id 'java-library'
    id 'idea'
}

group = project_package
version = project_version

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        inheritOutputDirs = true
    }
}

repositories {
    gradlePluginPortal() {
        content {
            plugin_portal_content.split(';').each {grp ->
                includeGroup grp
            }
        }
    }
    maven {
        name = 'Fabric'
        url = uri 'https://maven.fabricmc.net/'
        content {
            fabric_content.split(';').each {grp ->
                includeGroup grp
            }
        }
    }
    maven {
        name = 'Forge'
        url = uri 'https://maven.minecraftforge.net/'
        content {
            forge_content.split(';').each {grp ->
                includeGroup grp
            }
        }
    }
    maven {
        name = 'Garden of Fancy'
        url = uri 'https://maven.gofancy.wtf/releases'
        content {
            includeGroup 'wtf.gofancy.fancygradle'
        }
    }
    maven {
        name = 'ParchmentMC'
        url = uri 'https://maven.parchmentmc.org'
        content {
            parchment_content.split(';').each {grp ->
                includeGroup grp
            }
        }
    }
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation gradleTestKit()
    implementation "$loom_plugin:$loom_version"
    implementation "$shadow_plugin:$shadow_version"
    implementation "$forgegradle_plugin:$forgegradle_version"
    implementation "$foojay_plugin:$foojay_version"
    implementation "$parchment_forge_plugin:$parchment_forge_version"
    implementation "$mixingradle_plugin:$mixingradle_version"
    implementation "$fancygradle_plugin:$fancygradle_version"
}

compileJava {
    options.deprecation = false //We don't need to log all of the deprecated stuff when compiling
    options.encoding = 'UTF-8'
    options.release = 24
}

processResources {
    duplicatesStrategy = EXCLUDE
}

jar {
    duplicatesStrategy = EXCLUDE
    setArchiveBaseName project_id
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = "${project_id}.settings"
            implementationClass = "${project_package}.settings.SettingsPlugin"
        }
        //simplePlugin {
        //    id = "${project_id}.fabric"
        //    implementationClass = "${project_package}.fabric.FabricPlugin"
        //}
        //simplePlugin {
        //    id = "${project_id}.forge"
        //    implementationClass = "${project_package}.forge.ForgePlugin"
        //}
    }
}